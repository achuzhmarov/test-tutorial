buildscript {
    repositories {
        mavenCentral()
    }

    ext {
        springBootVersion = "2.0.3.RELEASE"
    }

    dependencies {
        classpath(
                "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        )
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.testRuntime
    }
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
        scopes.TEST.plus += [ configurations.integrationTestCompile ]
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

check.dependsOn integrationTest

bootJar {
    baseName = 'com.github.achuzhmarov'
    version =  '1.0-SNAPSHOT'
}

sourceCompatibility = 1.8

dependencies {
    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.hsqldb:hsqldb:2.4.1"

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testCompile "org.apache.httpcomponents:httpclient:4.5.5"
}